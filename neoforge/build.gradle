plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

repositories {
    maven { url 'https://jitpack.io' }
    maven { url = "https://api.modrinth.com/maven" }
}

dependencies {
    // Libraries
    forgeRuntimeLibrary(modImplementation("com.github.FiguraMC.luaj:luaj-core:$luaj-figura"))
    forgeRuntimeLibrary(modImplementation("com.github.FiguraMC.luaj:luaj-jse:$luaj-figura"))
    forgeRuntimeLibrary(modImplementation("com.neovisionaries:nv-websocket-client:$websocket"))

    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"

    //modImplementation("org.figuramc:figura-neoforge:$figura_version+$minecraft_version")
    modImplementation("maven.modrinth:figura:0.1.5b+1.21.4-neoforge")
    modRuntimeOnly "maven.modrinth:simple-voice-chat:neoforge-$minecraft_version-$svc_version"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

processResources {
    Map<String, Object> properties = new HashMap<>()

    properties.put("version", project.version)
    properties.put("java_version", rootProject.java_version)
    properties.put("minecraft_version", rootProject.minecraft_version)
    properties.put("figura_version", rootProject.figura_version)
    properties.put("mod_id", rootProject.plugin_id)
    properties.put("display_name", rootProject.display_name)
    properties.put("description", rootProject.description)
    properties.put("authors", rootProject.authors)
    properties.put("svc_version", rootProject.svc_version)

    properties.forEach((k, v) -> inputs.property(k, v.toString()))
    filesMatching("META-INF/neoforge.mods.toml") {
        expand properties
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier =  "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier =  null
}

jar {
    archiveClassifier =  "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
